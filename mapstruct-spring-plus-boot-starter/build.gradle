

apply plugin: "maven-publish"
apply plugin: "signing"

java {
    withJavadocJar()
    withSourcesJar()
}


jar{
    archiveFileName = 'mapstruct-spring-plus-boot-starter.jar'
}


dependencies {
    compileOnly "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    compile project(":mapstruct-spring-plus-core")
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mapstruct-spring-plus-boot-starter'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Mapstruct Spring Plus Spring Boot Starter'
                description = 'Spring MapStruct plus'
                url = ''
                packaging = 'jar'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ZhaoRd/mapstruct-spring-plus/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'zhaorende'
                        email = '1260825783@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ZhaoRd/mapstruct-spring-plus.git'
                    developerConnection = 'scm:git:https://github.com/ZhaoRd/mapstruct-spring-plus.git'
                    url = 'https://github.com/ZhaoRd/mapstruct-spring-plus'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}